# Licensed to the Apache Software Foundation (ASF) under one or more
# contributor license agreements. See the NOTICE file distributed with this
# work for additional information regarding copyright ownership. The ASF
# licenses this file to You under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.
scripts=[\
"
create service user su with forced path /home/users/system/cq:services/myfeature
set ACL for su
    remove * on /var
end
set ACL on /var
    remove jcr:all for su
end
set principal ACL for su
    allow jcr:all on /var restriction(rep:ntNames,nt:unstructured)
end",\
"
create group gr1 with path my/group/path
set repository ACL for gr1
    allow jcr:namespaceManagement,jcr:nodeTypeDefinitionManagement
end",\
"
set ACL on /conf,/content
    allow jcr:read,jcr:modifyProperties for gr1 restriction(rep:glob,*)
    allow jcr:read for gr1 restriction(rep:itemNames,jcr:primaryType,jcr:mixinTypes) restriction(rep:ntNames\,nt:folder)
end",\
"
set ACL for gr1 (ACLOptions\=someOption,someOtherOption,namespaced:option)
    deny jcr:versionManagement on /content nodetypes nt:folder restriction(rep:glob,/subtree)
    remove jcr:lockManagement on /content
    remove * on :repository,home(gr1)
end",\
"
create group gr2 with forced path /home/groups/myfeature
set ACL on /conf,/libs
    allow jcr:read,jcr:write for gr1,gr2 restriction(rep:glob,/subtree)
    remove jcr:modifyProperties for gr2
end",\
"
create group gr3
create user a
create user b with path myfeature
create user c with forced path /home/users/bla with password plaintext
create user d with password {SHA-256} dc460da4ad72c482231e28e688e01f2778a88ce31a08826899d54ef7183998b5
add a,b,c,d to group gr3
remove a,b from group gr1
disable service user deprecated_service_user : \"Disabled user to make an example\"
delete service user deprecated_service_user
delete user c
delete group gr1",\
"
create path /test(sling:Folder)/a(nt:folder mixin mix:referenceable,mix:shareable)/b(nt:unstructured)/c(sling:Folder mixin mix:created)
",\
"
register namespace ( prefix ) http://prefix/v0.0.0",\
"register nodetypes
<<\=\=\=
<<  <slingevent\=\'http://sling.apache.org/jcr/event/1.0\'>
<<  [slingevent:Event] > nt:unstructured, nt:hierarchyNode
<<    - slingevent:topic (string)
<<    - slingevent:properties (binary)
\=\=\=>>",\
"
register abstract privilege privAbstract
register privilege priv1
register privilege priv2 with privAbstract,priv1"\
]

